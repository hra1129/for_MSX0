100 REM SAVE"SYSMENU.BAS"
110 _KANJI
120 _TURBO ON
130 DEFINTA-Z:DIM S$(8),GX(8),GY(8)
140 CLS:COLOR 1,15,1:KEY OFF:SCREEN 5:COLOR=(14,3,3,3)
150 FOR I=0 TO 8:COLOR=(4+I,7,7,7):NEXT I
160 W=68:H=56
170 REM Top menu view
180 EX=0
190 S$(0)="Disk"
200 S$(1)="Reboot"
210 S$(2)=""
220 S$(3)="Volume"
230 S$(4)="Bright"
240 S$(5)="Info"
250 S$(6)="Exit"
260 S$(7)=""
270 S$(8)=""
280 GOSUB 730
290 D$="host/ip":GOSUB 2010:LOCATE 11,10:PRINT "ip  :";R$
300 D$="host/battery":GOSUB 2040:LOCATE 11,11:PRINT "batt:";R;"%"
310 GOSUB 870
320 IF P=-1 THEN 290
330 GOSUB 1090
340 ON P+1 GOTO 370,410,360,390,400,430,440,360,360
350 IF EX<>0 THEN RETURN
360 GOTO 170
370 TY=2:N$="msx/u0/drive/a":DN$="Unit0 Disk Image":LN$="host/media/dsk":IF LN$<>"" THEN FC=0:GOSUB 2110
380 GOSUB 560:GOTO 360
390 TY=1:N$="conf/sound_volume":DN$="Volume":LN$="":GOSUB 560:GOTO 360
400 TY=1:N$="conf/brightness":DN$="Brightness":LN$="":GOSUB 560:GOTO 360
410 GOSUB 460:IF OK=0 THEN 360
420 D$="msx/u0/pm/reboot":S=1:GOSUB 2090:GOTO 360
430 GOSUB 1590:GOTO 280
440 GOSUB 460:IF OK=0 THEN 360
450 GOTO 3010
460 REM Ok/Cancel view
470 EX=0:OK=0:FOR I=0 TO 8:S$(I)="":NEXT I:S$(7)="CANCEL":S$(8)="OK"
480 GOSUB 730
490 GOSUB 890
500 GOSUB 1090
510 ON P+1 GOTO 530,530,530,530,530,530,530,540,550
520 IF EX<>0 THEN RETURN
530 GOTO 490
540 EX=1:OK=0:GOTO 520
550 EX=1:OK=1:GOTO 520
560 REM Param setting view
570 EX=0
580 S$(0)="#"+DN$
590 S$(1)=""
600 S$(2)=""
610 S$(3)=""
620 S$(4)=""
630 S$(5)=""
640 S$(6)="-"
650 S$(7)="+"
660 S$(8)="OK"
670 GOSUB 730
680 GOSUB 820
690 GOSUB 890
700 GOSUB 960
710 IF EX<>0 THEN RETURN
720 GOTO 680
730 REM Show button
740 CLS:LL=0
750 FOR I=0 TO 8:GX(I)=11*(I MOD 3)+1:GY(I)=4*(I\3)+2:NEXT I
760 FOR I=0 TO 8:IF S$(I)="" THEN 800
770 IF LEFT$(S$(I),1)="#" THEN D$=RIGHT$(S$(I),LEN(S$(I))-1):GOTO 790 ELSE D$=S$(I)
780 LINE(GX(I)*8,GY(I)*16)-STEP(W,H),4+I,BF:LINE(GX(I)*8,GY(I)*16+H)-STEP(W,0),14:LINE(GX(I)*8+W,GY(I)*16)-STEP(0,H),14
790 LOCATE GX(I)+1,GY(I)+1:COLOR1,4+I:PRINT D$
800 NEXT I:COLOR,15
810 RETURN
820 REM Show value
830 IF TY=1 THEN D$=N$:GOSUB 2040:V=R:D$=STR$(V):I=2 ELSE D$=N$:GOSUB 2010:D$=R$:I=3:FOR J=0 TO FC:P=J:GOSUB 2190:IF OP$=V$ THEN V=J ELSE NEXT J
840 LOCATE GX(I)+2,GY(I)+1:PRINT SPACE$(LL):LL=LEN(D$)
850 LOCATE GX(I)+2,GY(I)+1:COLOR1,4+I:PRINT D$:COLOR,15
860 RETURN
870 REM Check for touch
880 IF PAD(4)<>0 THEN 930 ELSE P=-1:RETURN
890 REM Wait for touch
900 IF PAD(4)<>0 THEN 930
910 IF STRIG(0)<>0 THEN END
920 GOTO 890
930 X=PAD(5):Y=PAD(6):FOR I=0 TO 8:IF X>GX(I)*8 AND X<GX(I)*8+W AND Y>GY(I)*16 AND Y<GY(I)*16+H AND S$(I)<>"" THEN P=I:GOTO 950 ELSE NEXT I
940 IF PAD(4)=0 THEN 890 ELSE 940
950 RETURN
960 REM Button Event
970 GOSUB 1090
980 ON P+1 GOTO 940,940,940,940,940,940,990,1000,1060
990 NV=V-1:GOTO 1010
1000 NV=V+1:GOTO 1010
1010 IF TY=1 THEN V=NV:D$=N$:S=V:GOSUB 2090:GOTO 1050
1020 IF NV<0 THEN 1050
1030 P=NV:GOSUB 2190:IF OP$="" THEN 1050
1040 V=NV:D$=N$:P=V:GOSUB 2190:S$=OP$:GOSUB 2070
1050 RETURN
1060 REM Exit
1070 EX=1:D$="conf/save":S=1:GOSUB 2090
1080 RETURN
1090 REM Button Animation
1100 FOR J=4 TO 7:J2=J*2-7:J2=J2+(J2<0)*J2:COLOR=(4+I,J2,J,7):FOR K=0 TO 5:NEXT K:NEXT J:RETURN
1500 REM send string
1510 OUT8,&HC0:L=LEN(D$):OUT8,L:FOR I=1 TO L:OUT8,ASC(MID$(D$,I,1)):NEXT I:OUT8,0:R=INP(8):RETURN
1580 REM INFO.BAS
1590 CLS:C=0
1600 D$="host/name":GOSUB 2010:PRINT "name : ";R$
1610 D$="host/sw_version":GOSUB 2010:PRINT "sw   : ";R$
1620 D$="host/ip":GOSUB 2010:PRINT "ip   : ";R$
1630 LOCATE 0,5
1640 D$="host/heap":GOSUB 2040:PRINT "heap : ";R;"KB "
1650 D$="host/battery":GOSUB 2040:PRINT "batt : ";R;"% "
1660 IF PAD(4)=0 THEN FOR I=0 TO 100:NEXT I:C=C+1:IF C>20 THEN C=0:GOTO 1630 ELSE 1660
1670 RETURN
2000 REM _IOTGET( D$, R$ )
2010 OUT8,&HE0:OUT8,1:OUT8,&H53:GOSUB 1510:OUT8,&HE0:OUT8,1:OUT8,3:OUT8,&H80
2020 L=INP(8):R$="":FORI=1TOL:R$=R$+CHR$(INP(8)):NEXTI:RETURN
2030 REM _IOTGET( D$, R )
2040 OUT8,&HE0:OUT8,1:OUT8,&H53:GOSUB 1510:OUT8,&HE0:OUT8,1:OUT8,1:OUT8,&H80
2050 R=INP(8):P=VARPTR(R):POKEP,INP(8):POKEP+1,INP(8):RETURN
2060 REM _IOTPUT( D$, S$ )
2070 OUT8,&HE0:OUT8,1:OUT8,&H53:GOSUB 1510:OUT8,&HE0:OUT8,1:OUT8,&H43:D$=S$:GOTO 1510
2080 REM _IOTPUT( D$, S )
2090 OUT8,&HE0:OUT8,1:OUT8,&H53:GOSUB 1510:OUT8,&HE0:OUT8,1:OUT8,&H41:P=VARPTR(S):OUT8,&HC0:OUT8,2:OUT8,PEEK(P):OUT8,PEEK(P+1):RETURN
2100 REM _IOTFIND( LN$, FC )
2110 D$=LN$:OUT8,&HE0:OUT8,1:OUT8,&H53:GOSUB 1510:OUT8,&HE0:OUT8,1:OUT8,&H11:OUT8,&H80:FC=INP(8):P=VARPTR(FC):POKEP,INP(8):POKEP+1,INP(8)
2120 REM _IOTFIND( LN$, OP$(0), FC )
2130 D$=LN$:OUT8,&HE0:OUT8,1:OUT8,&H53:GOSUB 1510:OUT8,&HE0:OUT8,1:OUT8,&H13:OUT8,&H80:P=0
2140 L=INP(8):R$="":FOR I=1 TO L:R$=R$+CHR$(INP(8)):NEXT I:GOSUB 2170:P=P+1:IF P<=FC GOTO 2140
2150 RETURN
2160 REM OP$(P) = R$
2170 VP=(P+212)*256:L=LEN(R$):VPOKE VP,L:FOR J=1 TO L:VPOKE VP+J,ASC(MID$(R$,J,1)):NEXT J:RETURN
2180 REM OP$ = OP$(P)
2190 VP=(P+212)*256:L=VPEEK(VP):OP$="":FOR K=1 TO L:OP$=OP$+CHR$(VPEEK(VP+K)):NEXT K:RETURN
3000 REM END
3010 _TURBO OFF
3020 _ANK:SCREEN 1:COLOR:COLOR 15,4,7:END
